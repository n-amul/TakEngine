# ------------------------------------------------------------
#   TakEngine ‑ Engine library
# ------------------------------------------------------------

file(GLOB_RECURSE ENGINE_SRC CONFIGURE_DEPENDS 
src/*.cpp
src/*.hpp
)

add_library(engine SHARED ${ENGINE_SRC})
target_compile_features(engine PUBLIC cxx_std_17)

# Export headers for consumers (build tree + install tree)
target_include_directories(engine
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/src>
        $<INSTALL_INTERFACE:include>
)

target_compile_definitions(engine PRIVATE TEXPORT)
if(MSVC)
    target_compile_options(engine PRIVATE /wd4996)  
    target_compile_options(engine PRIVATE "/MP")
endif()

# ------------------------------------------------------------
#   Third‑party dependencies
# ------------------------------------------------------------

find_package(Vulkan REQUIRED)      # system Vulkan SDK

include(FetchContent)

# ---- GLM ----
FetchContent_Declare(
    glm
    GIT_REPOSITORY https://github.com/g-truc/glm.git
    GIT_TAG        0.9.9.8
)
FetchContent_MakeAvailable(glm)

# ---- GLFW (static) ----
set(BUILD_SHARED_LIBS OFF CACHE BOOL "" FORCE)
set(GLFW_BUILD_EXAMPLES OFF CACHE BOOL "" FORCE)
set(GLFW_BUILD_TESTS OFF CACHE BOOL "" FORCE)
set(GLFW_BUILD_DOCS OFF CACHE BOOL "" FORCE)
FetchContent_Declare(
    glfw
    GIT_REPOSITORY https://github.com/glfw/glfw.git
    GIT_TAG        3.4
)
FetchContent_MakeAvailable(glfw)

# ---- spdlog (header-only by default) ----
FetchContent_Declare(
    spdlog
    GIT_REPOSITORY https://github.com/gabime/spdlog.git
    GIT_TAG        v1.12.0
)
FetchContent_MakeAvailable(spdlog)

#----stb_image (single header)------------
# file(DOWNLOAD 
#     https://raw.githubusercontent.com/nothings/stb/master/stb_image.h
#     ${CMAKE_CURRENT_BINARY_DIR}/external/stb/stb_image.h
# )

# add_library(stb_image INTERFACE)
# target_include_directories(stb_image INTERFACE ${CMAKE_CURRENT_BINARY_DIR}/external/stb)

# ---- tinygltf ----
FetchContent_Declare(
    tinygltf
    GIT_REPOSITORY https://github.com/syoyo/tinygltf.git
    GIT_TAG        v2.8.21   # pick a stable release
)
FetchContent_MakeAvailable(tinygltf)

# ---- simdjson ----
FetchContent_Declare(
    simdjson
    GIT_REPOSITORY https://github.com/simdjson/simdjson.git
    GIT_TAG        v3.10.1  
)
FetchContent_MakeAvailable(simdjson)

# ---- ZSTD (required for KTX2 support) ----
FetchContent_Declare(
    zstd
    GIT_REPOSITORY https://github.com/facebook/zstd.git
    GIT_TAG        v1.5.5
    SOURCE_SUBDIR  build/cmake
)
set(ZSTD_BUILD_PROGRAMS OFF CACHE BOOL "" FORCE)
set(ZSTD_BUILD_SHARED OFF CACHE BOOL "" FORCE)
set(ZSTD_BUILD_STATIC ON CACHE BOOL "" FORCE)
set(ZSTD_BUILD_TESTS OFF CACHE BOOL "" FORCE)
FetchContent_MakeAvailable(zstd)

#----basis_universal-----
include(FetchContent)
FetchContent_Declare(
  basis_universal
  GIT_REPOSITORY https://github.com/BinomialLLC/basis_universal.git
  GIT_TAG        master
)
FetchContent_MakeAvailable(basis_universal)
# Manually create a target for the transcoder
add_library(basisu_transcoder STATIC
    ${basis_universal_SOURCE_DIR}/transcoder/basisu_transcoder.cpp
)
target_include_directories(basisu_transcoder PUBLIC
    ${basis_universal_SOURCE_DIR}/transcoder
)

# Link ZSTD directly to basisu_transcoder
target_link_libraries(basisu_transcoder PRIVATE libzstd_static)


# ---- ImGui ----
FetchContent_Declare(
    imgui
    GIT_REPOSITORY https://github.com/ocornut/imgui.git
    GIT_TAG        v1.91.5  # or use 'docking' branch for docking features
)
FetchContent_MakeAvailable(imgui)

# Create ImGui library with core + backends
add_library(imgui STATIC
    ${imgui_SOURCE_DIR}/imgui.cpp
    ${imgui_SOURCE_DIR}/imgui_demo.cpp
    ${imgui_SOURCE_DIR}/imgui_draw.cpp
    ${imgui_SOURCE_DIR}/imgui_tables.cpp
    ${imgui_SOURCE_DIR}/imgui_widgets.cpp
    ${imgui_SOURCE_DIR}/backends/imgui_impl_glfw.cpp
    ${imgui_SOURCE_DIR}/backends/imgui_impl_vulkan.cpp
)

target_include_directories(imgui PUBLIC
    ${imgui_SOURCE_DIR}
    ${imgui_SOURCE_DIR}/backends
)

# ImGui needs GLFW and Vulkan
target_link_libraries(imgui PUBLIC
    glfw
    Vulkan::Vulkan
)

# -------------defines--------------
target_compile_definitions(engine PRIVATE
    SHADER_DIR="${CMAKE_SOURCE_DIR}/shaders"
    TEXTURE_DIR="${CMAKE_SOURCE_DIR}/resources/textures"
    MODEL_DIR="${CMAKE_SOURCE_DIR}/resources/models"
)
# Enable texture format support including KTX2
target_compile_definitions(basisu_transcoder PRIVATE
    BASISU_TRANSCODER_SUPPORT_BC7=1
    BASISU_TRANSCODER_SUPPORT_BC3=1
    BASISU_TRANSCODER_SUPPORT_KTX2=1        # Enable KTX2
    BASISU_TRANSCODER_SUPPORT_KTX2_ZSTD=1   # Enable ZSTD compression
)

# ---- Link everything into engine.dll ----
target_link_libraries(engine
    PUBLIC
        Vulkan::Vulkan   # include & link flags
        glm::glm         # header‑only
        glfw             # static .lib
        spdlog::spdlog   # header‑only
        tinygltf            
        simdjson::simdjson
        basisu_transcoder
        imgui  
)
